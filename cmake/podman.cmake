function(registerPod)
  cmake_parse_arguments(REGISTER_POD "" "TWEAK_IMAGE;DEPENDS_ON_POD" "CONFIGURED_DEPENDENCIES;CONFIGURED_DEPENDENCIES_COPYONLY;DEPENDENCIES;BUILT_DEPENDENCIES;USED_VARIABLES" ${ARGN} )

  configure_file(Dockerfile.in ${CMAKE_CURRENT_BINARY_DIR}/Dockerfile @ONLY)
  file(SHA1 ${CMAKE_CURRENT_BINARY_DIR}/Dockerfile DOCKERFILE_SHA1)

  foreach(DEP ${REGISTER_POD_DEPENDENCIES})
    file(SHA1 ${CMAKE_CURRENT_SOURCE_DIR}/${DEP} DOCKERFILE_DEPENDENCY_SHA1)
    string(SHA1 DOCKERFILE_SHA1 "${DOCKERFILE_SHA1} ${DOCKERFILE_DEPENDENCY_SHA1}")
  endforeach()
  
  if(REGISTER_POD_DEPENDS_ON_POD)
    string(SHA1 DOCKERFILE_SHA1 "${DOCKERFILE_SHA1} ${REGISTER_POD_DEPENDS_ON_POD}")
    set(POD_DEPENDENCY "pod-${REGISTER_POD_DEPENDS_ON_POD}")
  endif()

  if(REGISTER_POD_TWEAK_IMAGE)
    configure_file(${REGISTER_POD_TWEAK_IMAGE} ${REGISTER_POD_TWEAK_IMAGE} @ONLY)
    file(SHA1 ${CMAKE_CURRENT_SOURCE_DIR}/${REGISTER_POD_TWEAK_IMAGE} TWEAKFILE_DEPENDENCY_SHA1)
    string(SHA1 DOCKERFILE_SHA1 "${DOCKERFILE_SHA1} ${TWEAKFILE_DEPENDENCY_SHA1}")
  endif()

  foreach(FILE ${REGISTER_POD_CONFIGURED_DEPENDENCIES_COPYONLY})
    configure_file(${FILE} ${FILE} COPYONLY)
    list(APPEND REGISTER_POD_DEPENDENCIES ${CMAKE_CURRENT_BINARY_DIR}/${FILE})
    file(SHA1 ${CMAKE_CURRENT_BINARY_DIR}/${FILE} DOCKERFILE_DEPENDENCY_SHA1)
    string(SHA1 DOCKERFILE_SHA1 "${DOCKERFILE_SHA1} ${DOCKERFILE_DEPENDENCY_SHA1}")
  endforeach()

  foreach(FILE ${REGISTER_POD_CONFIGURED_DEPENDENCIES})
    configure_file(${FILE} ${FILE} @ONLY)
    list(APPEND REGISTER_POD_DEPENDENCIES ${CMAKE_CURRENT_BINARY_DIR}/${FILE})
    file(SHA1 ${CMAKE_CURRENT_BINARY_DIR}/${FILE} DOCKERFILE_DEPENDENCY_SHA1)
    string(SHA1 DOCKERFILE_SHA1 "${DOCKERFILE_SHA1} ${DOCKERFILE_DEPENDENCY_SHA1}")
  endforeach()

  string(SHA1 DOCKERFILE_SHA1 "${DOCKERFILE_SHA1} ${REGISTER_POD_USED_VARIABLES}")

  file(WRITE ${CMAKE_CURRENT_BINARY_DIR}/pod-checksum ${DOCKERFILE_SHA1})

  set(POD_TARGET pod-${DOCKERFILE_SHA1})  
  set(POD_TARGET ${POD_TARGET} PARENT_SCOPE)
  set(POD_URI ${PODMAN_REGISTRY}/generic:${DOCKERFILE_SHA1} PARENT_SCOPE)

  if (NOT TARGET ${POD_TARGET})
    configure_file(${CMAKE_SOURCE_DIR}/cmake/runPodman.sh.in ${CMAKE_CURRENT_BINARY_DIR}/runPodman.sh)
    
    add_custom_command(
      COMMENT "Provide Podman for ${BUILD_TASK} / ${DOCKERFILE_SHA1} depends on ${POD_DEPENDENCY}"
      OUTPUT .podman-${DOCKERFILE_SHA1}
      DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/Dockerfile
      DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/runPodman.sh
      DEPENDS ${CMAKE_SOURCE_DIR}/cmake/runPodman.sh.in
      DEPENDS ${REGISTER_POD_DEPENDENCIES}
      DEPENDS ${REGISTER_POD_BUILT_DEPENDENCIES}
      DEPENDS ${REGISTER_POD_TWEAK_IMAGE} 
      DEPENDS ${POD_DEPENDENCY}
      VERBATIM
      COMMAND ${CMAKE_CURRENT_BINARY_DIR}/runPodman.sh ${CMAKE_CURRENT_BINARY_DIR}/${REGISTER_POD_TWEAK_IMAGE}
      COMMAND touch .podman-${DOCKERFILE_SHA1}
      )
    
    message("Add target ${POD_TARGET} for ${CMAKE_CURRENT_SOURCE_DIR}")
    add_custom_target(${POD_TARGET} DEPENDS .podman-${DOCKERFILE_SHA1})
  endif()
endfunction()
